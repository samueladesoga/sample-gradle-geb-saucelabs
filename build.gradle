import org.apache.tools.ant.taskdefs.condition.Os
import geb.gradle.saucelabs.SauceAccount

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.gebish:geb-gradle:2.2'
    }
}

ext {
    // The drivers we want to use
    drivers = ["firefox", "chrome"]

    ext {
        groovyVersion = '2.4.12'
        httpBuilderVersion = '0.7'
        gebVersion = '2.2'
        seleniumVersion = '3.6.0'
        chromeDriverVersion = '2.36'
    }
}

apply plugin: "groovy"
apply from: "gradle/idea.gradle"
apply from: "gradle/osSpecificDownloads.gradle"
apply plugin: "geb-saucelabs"

repositories {
    mavenCentral()
    maven { url "http://repository-saucelabs.forge.cloudbees.com/release" }
}

dependencies {
    // If using Spock, need to depend on geb-spock
    testCompile "org.gebish:geb-spock:$gebVersion"
    testCompile("org.spockframework:spock-core:1.0-groovy-2.4") {
        exclude group: "org.codehaus.groovy"
    }
    testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"
    testCompile "org.codehaus.groovy.modules.http-builder:http-builder:$httpBuilderVersion"

    // If using JUnit, need to depend on geb-junit (3 or 4)
    testCompile "org.gebish:geb-junit4:$gebVersion"

    // Drivers
    testCompile "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
    testCompile "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"

    sauceConnect "com.saucelabs:sauce-connect:3.0.28"
    sauceConnect "com.saucelabs:ci-sauce:1.81"
}

drivers.each { driver ->
    task "${driver}Test"(type: Test) {
        reports {
            html.destination = reporting.file("$name/tests")
            junitXml.destination = file("$buildDir/test-results/$name")
        }

        outputs.upToDateWhen { false }  // Always run tests

        systemProperty "geb.build.reportsDir", reporting.file("$name/geb")
        systemProperty "geb.env", driver
    }
}

chromeTest {
    dependsOn unzipChromeDriver

    def chromedriverFilename = Os.isFamily(Os.FAMILY_WINDOWS) ? "chromedriver.exe" : "chromedriver"
    systemProperty "webdriver.chrome.driver", new File(unzipChromeDriver.outputs.files.singleFile, chromedriverFilename).absolutePath
}

sauceLabs {
    useTunnel = false
    browsers {
        chrome_mac
    }
    task {
        testClassesDir = test.testClassesDir
        testSrcDirs = test.testSrcDirs
        classpath = test.classpath
    }

    account {
        //username = System.getenv(SauceAccount.USER_ENV_VAR)
        //accessKey = System.getenv(SauceAccount.ACCESS_KEY_ENV_VAR)
        username = System.getenv("SAUCE_USERNAME")
        accessKey = System.getenv("SAUCE_ACCESS_KEY")
    }


}

test {
    dependsOn drivers.collect { tasks["${it}Test"] }
    enabled = false
}

apply from: "gradle/ci.gradle"
